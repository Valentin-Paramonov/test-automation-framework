import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'de.undercouch.download' version '3.0.0'
    id 'com.github.jacobono.jaxb' version '1.3.5'
}

apply plugin: 'groovy'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
}

task downloadFreemindSchema(type: Download) {
    src 'http://freemind.cvs.sourceforge.net/viewvc/freemind/freemind/freemind.xsd'
    dest buildDir
}

jaxb {
    final buildDirName = project.buildDir.name
    xsdDir = buildDirName
    episodesDir = buildDirName
    xjc {
        generatePackage = 'freemind'
    }
}

xjc.dependsOn downloadFreemindSchema
compileJava.dependsOn xjc